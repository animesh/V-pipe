import logging
import glob
import os
import shutil
import configparser
from pathlib import Path
from itertools import product

LOGGER = logging.getLogger('snakemake.logging')

if not 'VPIPE_BASEDIR' in dir():
    VPIPE_BASEDIR = workflow.basedir
LOGGER.info('VPIPE_BASEDIR = %s', VPIPE_BASEDIR)

# Include config file parser and globals
VPIPE_BENCH = True
include: "rules/benchmark.smk"
functions = srcdir("functions.sh")

ALIGNERS = config.benchmark['aligners'].split(',')
SNV_CALLERS = config.benchmark['snv_callers'].split(',')
BENCH_RUNS = list(product(ALIGNERS, SNV_CALLERS))

configs = {}
for opts in BENCH_RUNS:
    dirname = '-'.join(opts)
    configs[dirname] = VPIPE_CONFIG()
    configs[dirname].general['aligner'] = opts[0]
    configs[dirname].general['snv_caller'] = opts[1]

# DUMMY RULE
rule allbenchRunner:
    input:
        expand("simulated_data/{dataset}/raw_data/simreads_R1.fastq",
               dataset=datasets),
        expand("simulated_data/{dataset}/raw_data/simreads_R2.fastq",
               dataset=datasets) if config.input['paired'] else [],
        expand("{bench_dir}/variants/SNV_calling_performance.tsv",
               bench_dir=configs.keys())

rule run_simBench:
    input:
        CONFIG_FILE = "vpipe.config",
        SAMPLES = config.input['samples_file'],
    output:
        R1 = expand("simulated_data/{dataset}/raw_data/simreads_R1.fastq",
                    dataset=datasets),
        R2 = expand("simulated_data/{dataset}/raw_data/simreads_R2.fastq",
                    dataset=datasets) if config.input['paired'] else [],
    params:
        scratch = '1250',
        mem = config.run_simBench['mem'],
        time = config.run_simBench['time'],
        BASEDIR = VPIPE_BASEDIR,
        SNAKEMAKE_OPTIONS = lambda wildcards: config.benchmark['snakemake_options'],
        SNAKEMAKE_OPTS_ENVVAR = lambda wildcards: os.environ.get('SNAKEMAKE_OPTS', ''),
        SNAKEMAKE = config.applications['snakemake'],
    log:
        outfile = "logs/run_simBench.out.log",
        errfile = "logs/run_simBench.err.log",
    threads:
        1
    shell:
        """
        # simbolink links: samples-file and config file
        ln -s {input.CONFIG_FILE} simulated_data/
        ln -s {input.SAMPLES} simulated_data/

        # copy reference file(s). Three expected locations: (1) current
        # directory, (2) one level above, (3) references sub-directory
        # Alternative: read references from samples-file and config, and move
        # any reference file located in the references sub-directory (e.g.,
        # cohort_consensus.fasta)
        REFERENCES=$( find . -type f \( -iname \*.fasta -o -iname \*.fa \) )
        for i in ${{REFERENCES}}; do
            DIRNAME=$(diranme $i)
            BASENAME=$(basename $i)
            mkdir -p simulated_data/${{DIRNAME}}
            ln -s ${{i}} simulated_data/${{DIRNAME}}
        done
        #if [[ -f *.fasta ]]; then
        #    cp *.fasta simulated_data/
        #fi

        #if [[ -f references/*.fasta ]]; then
        #    mkdir -p simulated_data/references
        #    cp references/*.fasta simulated_data/references
        #fi

        # change to working directory
        cd simulated_data/

        if [[ -z "{params.SNAKEMAKE_OPTS_ENVVAR}" ]]; then
            {params.SNAKEMAKE} -s {params.BASEDIR}/vpipeBench.snake simulated_data \
                {params.SNAKEMAKE_OPTIONS}
        else
            {params.SNAKEMAKE} -s {params.BASEDIR}/vpipeBench.snake simulated_data \
                {params.SNAKEMAKE_OPTS_ENVVAR}
        fi
        """

localrules:
    prepare_run
rule prepare_run:
    input:
        R1 = expand("simulated_data/{dataset}/raw_data/simreads_R1.fastq",
                    dataset=datasets),
        R2 = expand("simulated_data/{dataset}/raw_data/simreads_R2.fastq",
                    dataset=datasets) if config.input['paired'] else [],
    output:
        CONFIG_FILE = "{bench_dir}/vpipe.config",
        SAMPLES = "{bench_dir}" / Path(config.input['samples_file']),
        REF = "{bench_dir}" / Path(reference_file),
        R1 = expand("{bench_dir}/{dataset}/raw_data/simreads_R1.fastq",
                    dataset=datasets, allow_missing=True),
        R2 = expand("{bench_dir}/{dataset}/raw_data/simreads_R2.fastq",
                    dataset=datasets, allow_missing=True) if config.input['paired'] else [],
        HAPLOTYPE_REFs = expand(
            "{bench_dir}/{dataset}/references/haplotypes/haplotypes.fasta",
            dataset=datasets, allow_missing=True),
    log:
        outfile = "logs/{bench_dir}_prepare_run.out.log",
        errfile = "logs/{bench_dir}_prepare_run.err.log",
    run:
        # Copy reference file(s). Two expected locations: (1) current
        # directory, or (2) one level above.
        # Alternative: read references from samples-file and config, and move
        # any reference file located in the references sub-directory (e.g.,
        # cohort_consensus.fasta)
        if not config.simulate_haplotypes['use_master']:
            fasta_files = glob.glob("*/*.fasta")
            for file in fasta_files:
                dirname = os.path.dirname(file)
                if os.path.isfile(file):
                    dest = os.path.join(wildcards.bench_dir, dirname)
                    Path(dest).mkdir(parents=True, exist_ok=True)
                    shutil.copy(file, dest)
            fasta_files = glob.glob("*.fasta")
            for file in fasta_files:
                if os.path.isfile(file):
                    shutil.copy(file, wildcards.bench_dir)

        # Copy the config file with the necesary changes
        with open(output.CONFIG_FILE, 'w') as configfile:
            configs[wildcards.bench_dir].write(configfile)

        # Copy samples-file. Alternative: use symbolic links
        shutil.copy(config.input['samples_file'], output.SAMPLES)

        # Copy datadir
        shutil.copytree(config.input['datadir'], wildcards.bench_dir)


# TODO: handle special cases, e.g. dry-run (touch and clean files)
#       and clean rules
rule run_vpipeBench:
    input:
        "{bench_dir}/vpipe.config",
        "{bench_dir}" / Path(config.input['samples_file']),
    output:
        "{bench_dir}/variants/SNV_calling_performance.tsv"
    params:
        scratch = '1250',
        mem = config.run_vpipeBench['mem'],
        time = config.run_vpipeBench['time'],
        BASEDIR = VPIPE_BASEDIR,
        SNAKEMAKE_OPTIONS = lambda wildcards: config.benchmark['snakemake_options'],
        SNAKEMAKE_OPTS_ENVVAR = lambda wildcards: os.environ.get('SNAKEMAKE_OPTS', ''),
        SNAKEMAKE = config.applications['snakemake']
    log:
        outfile = "logs/{bench_dir}_vpipeBench.out.log",
        errfile = "logs/{bench_dir}_vpipeBench.err.log",
    threads:
        1
    shell:
        """
        # Change to working directory
        cd {wildcards.bench_dir}/

        if [[ -z "{params.SNAKEMAKE_OPTS_ENVVAR}" ]]; then
            {params.SNAKEMAKE}  -s {params.BASEDIR}/vpipeBench.snake \
                {params.SNAKEMAKE_OPTIONS}
        else
            {params.SNAKEMAKE} -s {params.BASEDIR}/vpipeBench.snake \
                {params.SNAKEMAKE_OPTS_ENVVAR}
        fi
        """
